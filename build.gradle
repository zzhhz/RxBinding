buildscript {
  ext.buildConfig = [
    'minSdkVersion': 21,
    'compileSdkVersion': 33,
  ]
  ext.versions = [
    'kotlin': '1.8.0',
  ]
  ext.deps = [
    'kotlinStdlib': "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",

    'androidXAnnotations': 'androidx.annotation:annotation:1.3.0',
    'androidXAppCompat': 'androidx.appcompat:appcompat:1.6.1',
    'androidXCore': 'androidx.core:core:1.9.0',
    'androidXDrawerLayout': 'androidx.drawerlayout:drawerlayout:1.0.0',
    'androidXLeanback': 'androidx.leanback:leanback:1.0.0',
    'androidXRecyclerView': 'androidx.recyclerview:recyclerview:1.0.0',
    'androidXSlidingPaneLayout': 'androidx.slidingpanelayout:slidingpanelayout:1.2.0',
    'androidXSwipeRefreshLayout': 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0',
    'androidXViewPager': 'androidx.viewpager:viewpager:1.0.0',
    'androidXViewPager2': 'androidx.viewpager2:viewpager2:1.0.0',

    'androidXTestCore': 'androidx.test:core:1.4.0',
    'androidXTestRunner': 'androidx.test:runner:1.4.0',
    'androidXTestRules': 'androidx.test:rules:1.4.0',
    'androidXTestEspresso': 'androidx.test.espresso:espresso-core:3.4.0',
    'androidXTestEspressoContrib': 'androidx.test.espresso:espresso-contrib:3.1.1',

    'googleMaterial': 'com.google.android.material:material:1.0.0',

    'rxJava': 'io.reactivex.rxjava3:rxjava:3.1.6',
    'rxAndroid': 'io.reactivex.rxjava3:rxandroid:3.0.2',

    'junit': 'junit:junit:4.12',
  ]

  dependencies {
    classpath 'com.android.tools.build:gradle:7.4.1'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
  }
  repositories {
    mavenCentral()
    google()
    jcenter()
  }
}

subprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  group = GROUP
  version = VERSION_NAME

  afterEvaluate {
    // The default 'assemble' task only applies to normal variants. Add test variants as well.
    def assembleProvider = tasks.named('assemble')
    android.testVariants.all { variant ->
      assembleProvider.configure {
        it.dependsOn(variant.assembleProvider)
      }
    }

    // Disable BuildConfig generation.
    // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
    android.libraryVariants.all {
      it.generateBuildConfigProvider.configure {
        enabled = false
      }
    }

    tasks.withType(com.android.build.gradle.internal.tasks.AndroidTestTask) { task ->
      task.doFirst {
        logging.level = LogLevel.INFO
      }
      task.doLast {
        logging.level = LogLevel.LIFECYCLE
      }
    }
  }
}
